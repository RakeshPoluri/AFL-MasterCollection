function ST(_H, _L, _C, Pd, Fr)
{
	global trend;
    H_L = _H - _L;
	H_C = abs(_H - Ref(_C, -1));
	L_C = abs(_L - Ref(_C, -1));
	_TR = Max(H_L, Max(H_C, L_C));
	_ATR = Wilders(_TR, Pd);
	//_ATR = ATR(Pd);
	Up=(_H+_L)/2+(Fr*_ATR);
	Dn=(_H+_L)/2-(Fr*_ATR);
	iATR=_ATR;
	SuperTrend=Null;
	trend[0]=1;
	changeOfTrend=0;
	flag=flagh=0;	
	for (i = 1; i <BarCount; i++)
	{
		SuperTrend[i] = Null;
		trend[i]=1;		
		if (_C[i]>Up[i-1])
		{
			trend[i]=1;
			if (trend[i-1] == -1) changeOfTrend = 1;
		}
		else if (_C[i]<Dn[i-1])
		{
			trend[i]=-1;
			if (trend[i-1] == 1) changeOfTrend = 1;
		}
		else if (trend[i-1]==1)
		{
			trend[i]=1;
			changeOfTrend = 0;
		}
		else if (trend[i-1]==-1)
		{
			trend[i]=-1;
			changeOfTrend = 0;
		}
		if (trend[i]<0 && trend[i-1]>0) flag=1;
		else flag=0;
		if (trend[i]>0 && trend[i-1]<0) flagh=1;
		else flagh=0;
		if (trend[i]>0 && Dn[i]<Dn[i-1]) Dn[i]=Dn[i-1];
		if (trend[i]<0 && Up[i]>Up[i-1]) Up[i]=Up[i-1];
		if (flag==1) Up[i]=(_H[i]+_L[i])/2+(Fr*iATR[i]);
		if (flagh==1) Dn[i]=(_H[i]+_L[i])/2-(Fr*iATR[i]);
		if (trend[i]==1)
		{
			SuperTrend[i]=Dn[i];
			if (changeOfTrend == 1) changeOfTrend = 0;
		}
		else if (trend[i]==-1)
		{
			SuperTrend[i]=Up[i];
			if (changeOfTrend == 1) changeOfTrend = 0;
		}
	}
	return SuperTrend;	
}

High_	= ParamField("High", 1);
Low_	= ParamField("Low", 2);
Close_	= ParamField("Close", 3);
Periods	= Param("ATR Periods", 21, 2, 500, 2);
Factor	= Param("ATR Multiple", 2, 0.5, 5, 0.25);

_st = ST(High_, Low_, Close_, Periods, Factor);

Plot(IIf(_st < Close, _st, Null), "", colorGreen, styleLine|styleThick);
Plot(IIf(_st > Close, _st, Null), "", colorRed, styleLine|styleThick);
PlotShapes(IIf(Cross(Close, _st), shapeSmallCircle, 0), colorGreen, 0.0, _st, 0);
PlotShapes(IIf(Cross(_st, Close), shapeSmallCircle, 0), colorRed, 0.0, _st, 0);

Plot(_st, "SuperTrend", IIf( trend == 1, colorGreen, colorRed ), styleThick|styleNoDraw);

//Plot(_st, "SuperTrend", IIf( trend == 1, colorGreen, colorRed ), styleThick);
